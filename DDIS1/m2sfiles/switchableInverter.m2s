**********************************
**** INVERTER: simple example
**********************************

* Simulation setup
*------------------
*Some simulation options
.options post nomod
.option opts fast parhier=local

*Add the 45nm CMOS library. Notice how the path is generated by MATLAB
.lib '$< strcat(pwd, '/Resources/Technology/tech_wrapper.lib') >$ ' tt

*Add the .vec file
.vec '$< strcat(pwd, '/m2sfiles/switchableInverter.vec')>$'

.tran 0.001n 10n *Do a transient simulation, final time = 10ns
.probe v i

.param supply = $globals.supply


* Voltage sources
*-----------------
Vdd vdd vss supply
Vss vss 0 0 


* Simulated circuit
*-------------------

$ for i = 0:15
    Xnot$i in$i sw out$i vdd vss MYXOR multfac=2
    Cap$i out$i vss 10f
$ end


* Subcircuits
*-------------
* xor-> ( (b*a')' * (b'*a)' )' using NAND gates
.SUBCKT MYXOR input control output   vdd vss multfac='1'    
      * first calculate inverses with NAND: 
      * MYNAND in1     in2     out     vdd vss multfac='1'
        XinvA  input   input   outinvA vdd vss MYNAND multfac=multfac 
        XinvB  control control outinvB vdd vss MYNAND multfac=multfac 
        * (b*a')'
        Xnand1 outinvA control outnand1 vdd vss MYNAND multfac=multfac 
        * (b'*a)'
        Xnand2 outinvB input   outnand2 vdd vss MYNAND multfac=multfac 
        * combine
        XnandOut outnand1 outnand2 output vdd vss MYNAND multfac=multfac 
.ENDS MYXOR

.SUBCKT MYNAND input1 input2 output vdd vss multfac='1'
*    drain  gate   src  bulk
xMn1 dn1    input1 vss  vss MOSN w='multfac*1.5*120e-9' l='45e-9' * extra factor b/c series -> extra node that has capacitance
xMn2 output input2 dn1  vss MOSN w='multfac*1.5*120e-9' l='45e-9'

xMp1 output input1 vdd  vdd MOSP w='multfac*120e-9' l='45e-9' * not *2 b/c pMOS in parallel for NAND -> only half size needed
xMp2 output input2 vdd  vdd MOSP w='multfac*120e-9' l='45e-9'
.ENDS MYNAND

.END



